;------------------+
; アイテム編集画面 |
;------------------+

*createEditDialog

	; アイテム編集画面の作成
	screen 3, 320, 240, 2
	cls
	setColor iniBGColor
	boxf 0, 0, 320, 240

	objmode 2, 1
	setColor iniFGColor

	pos 16, 16
	font iniFont, 16
	mes "名前"
	pos 56, 16
	font iniFont, 12
	editName = ""
	input editName, 248, 16, 32
	idEditName = stat

	pos 16, 40
	font iniFont, 16
	mes "場所"
	pos 56, 40
	font iniFont, 12
	editPath = ""
	input editPath, 208, 16, 1024
	idEditPath = stat
	pos 272, 38
	objsize 32, 20
	button gosub "参照", *callLocation
	idButtonEditPath = stat

	pos 16, 64
	font iniFont, 16
	mes "作業場所"
	pos 88, 64
	font iniFont, 12
	editWorkDir = ""
	input editWorkDir, 176, 16, 1024
	idEditWorkDir = stat
	pos 272, 62
	objsize 32, 20
	button gosub "参照", *callWorkDir
	idButtonEditWorkDir = stat

	pos 16, 88
	font iniFont, 16
	mes "種別"
	pos 56, 86
	font iniFont, 12
	editType = 0
	objsize 120, 20
	combox editType, 40, "実行形式\nファイル\nフォルダー\nサブメニュー\n管理者権限"
	idEditType = stat

	pos 16, 112
	font iniFont, 16
	mes "Icon"
	pos 56, 112
	font iniFont, 12
	editIcon = ""
	input editIcon, 208, 16, 1024
	idEditIcon = stat
	sendmsg objinfo(idEditIcon, 2), $CF, 1 ; 編集不可
	pos 272, 110
	objsize 32, 20
	button gosub "参照", *callIconLocation

	editIconIndex = 0
	editIconIndexMax = 1
	pos 56, 142
	font iniFont, 12
	objsize 16, 20
	button gosub "<", *decreaseIconIndex
	pos 112, 142
	font iniFont, 12
	objsize 16, 20
	button gosub ">", *increaseIconIndex

	pos 56, 176
	objsize 48, 32
	font iniFont, 16
	button gosub "登録", *registItem

	pos 112, 176
	objsize 48, 32
	font iniFont, 16
	button gosub "削除", *deleteItem
	idDeleteItem = stat

	pos 168, 176
	objsize 48, 32
	font iniFont, 16
	button gosub "複写", *copyingItem
	idCopyItem = stat

	pos 224, 176
	objsize 56, 32
	font iniFont, 16
	button gosub "ｷｬﾝｾﾙ", *cancelEditingItem

	return

*itemedit
	gsel 0
	EnableWindow hwnd, 0 ; 自ウィンドウを無効にする。

	gsel 3, -1
	width , , ginfo_mx - 160, ginfo_my - 120
	gsel 3, 2
	objprm idEditName, argName
	objprm idEditPath, argPath
	objprm idEditIcon, argIcon
	objprm idEditType, argType
	objprm idEditWorkDir, argWdir

	if argIcon != "" {
		gosub *repaintIcon
	}

	if (argIndex == -1) {
		objenable idDeleteItem, 0
		objenable idCopyItem, 0
	} else {
		objenable idDeleteItem, 1
		objenable idCopyItem, 1
	}
	editMode = 0
	onnstat = 0

	repeat
		getkey ev, 27
		if (ev == 1)|(oncstat == 1) :break

		if getTypeValue(editType) == "dir" { ; サブメニュー
			objenable idEditPath, 0
			objenable idButtonEditPath, 0
			if (argIndex == -1)|(itemType(i) != "dir") :objprm idEditPath, editName + ".dat"
		} else {
			objenable idEditPath, 1
			objenable idButtonEditPath, 1
		}
		if getTypeValue(editType) == "exe" {
			objenable idEditWorkDir, 1
			objenable idButtonEditWorkDir, 1
		} else {
			objenable idEditWorkDir, 0
			objenable idButtonEditWorkDir, 0
		}

		; 多重起動の通知があれば前面表示
		if ( onnstat == 1 ) {
			gsel 3, 2
			onnstat = 0
		}

		wait 5
	loop
	oncstat = 0

	gsel 3, -1
	gsel 0, 2
	EnableWindow hwnd, 1
	return

*callLocation
	if getTypeValue(editType) == "fold" {
		gsel 0, 1
		gsel 3, 1
		selfolder s, ""
		gsel 3, 2
		if stat == 0 {
			objprm idEditPath, s
			dialog "今設定したファイルを\nアイコンと作業場所にも\n反映しますか？", 2
			if stat == 6 :gosub *callIconLocationInner
		}
	} else {
		dialog "", 16
		if stat == 1 {
			s = refstr
			objprm idEditPath, s
			objprm idEditType, isNotExecutable( s )
	
			if getTypeValue(editType) == "exe" {
				dialog "今設定したファイルを\nアイコンと作業場所にも\n反映しますか？", 2
				if stat == 6 {
					gosub *callIconLocationInner
					s = getpathEx(s, 0)
					gosub *callWorkDirInner
				}
			} else {
				dialog "今設定したファイルを\nアイコンにも反映しますか？", 2
				if stat == 6 :gosub *callIconLocationInner
			}
		}
	}
	return

*callIconLocation
	dialog "", 16
	s = refstr
	if stat == 1 :gosub *callIconLocationInner
	return

*callIconLocationInner
	argIcon = s
	objprm idEditIcon, argIcon

	editIconIndex = 0
	gosub *repaintIcon
	return

*callWorkDir
	gsel 0, 1
	gsel 3, 1
	selfolder s, ""
	gsel 3, 2
	if stat == 0 :gosub *callWorkDirInner
	return

*callWorkDirInner
	objprm idEditWorkDir, s
	return

*repaintIcon
	editIconIndexMax = getIconIndexMax( argIcon )
	if ( editIconIndexMax == 0 ) {
		; アイコンを持たない実行ファイルは決め打ちで再設定する。既存のものも容赦なく。
		argIcon = dir_sys + "\\shell32.dll"
		objprm idEditIcon, argIcon
		editIconIndexMax = getIconIndexMax( argIcon )
		editIconIndex = 2
	}
	gosub *repaintIconInner
	return

*repaintIconInner
	setColor iniBGColor
	boxf 76, 136, 108, 168
	drawIcon2Hdc argIcon, 76, 136, editIconIndex
	redraw 1
	return

*decreaseIconIndex
	if ( isExeOrDll( editIcon ) ) {
		if ( editIconIndex > 0 ) {
			editIconIndex--
			gosub *repaintIconInner
		}
	}
	return

*increaseIconIndex
	if ( isExeOrDll( editIcon ) ) {
		if ( editIconIndex < ( editIconIndexMax - 1 )) {
			editIconIndex++
			gosub *repaintIconInner
		}
	}
	return

*registItem
	notMakeSubItem = 0
	if getTypeValue(editType) == "dir" { ; サブメニューであり、名前が重複している場合は登録しない
		chdir curdir
		exist "data\\" + EditPath
		if strsize >= 0 {
			dialog "この名前のサブメニューは登録済みです。\n同じものを呼び出しますか？", 3
			if stat == 7 :return
			notMakeSubItem = 1
		}
	}
	gosub *writedat
	gosub *getdat
	oncstat = 1
	return

*deleteItem
	if itemType(i) == "dir" {
		; サブメニューの場合は参照を精査する
		chdir curdir + "\\data"
		dirlist temp, "*.dat"
		split temp, "\n", array
		u = ""
		repeat stat - 1
			bload array(cnt), s
			t = strmid(s, 0, instr(s, 0, "\n"))
			if t == "parent="+itemPath(i) {
				u += "\n" + array(cnt)
			}
		loop
		if strlen(u) > 0 {
			dialog itemName(i) + "は以下のサブメニューから参照されているため、削除できません。" + u
			return
		}
	}
	dialog itemName(i) + "を削除します。よろしいですか？", 3
	if stat == 7 :return
	editMode = 1
	gosub *writedat
	if ( itemType(i) == "dir" )&( itemPath( i ) != "root.dat" ) { ; 該当サブメニューがルートでなければ削除可能
		dialog "サブメニューのファイル自体を削除しますか？\n残しておくと、再度参照できます。", 3
		if stat == 6 {
			chdir curdir + "\\data"
			dirlist temp, "*.bmp"
			delete itemPath( i )
			delete getpath( itemPath(i), 1 ) + ".bmp"
		}
	}
	gosub *getdat
	oncstat = 1
	return

*copyingItem
	dialog "複写したいサブメニューに移動して、\n空白のアイテムをクリック(新規アイテムを作成)してください。"

	copymode = 1
	copyName = editName
	copyPath = editPath
	copyIcon = editIcon
	copyType = editType
	copyIconIndex = editIconIndex
	copyWorkDir = editWorkDir

	buffer 6, 32, 32, 0
	gsel 6
	pos 0, 0
	gcopy 3, 76, 136, 32, 32
	gsel 3

	oncstat = 1
	return

*cancelEditingItem
	oncstat = 1
	return

;--------------+
; 設定編集画面 |
;--------------+

*createSettingDialog

	; 設定編集画面の作成
	screen 4, 320, 280, 2
	cls
	setColor iniBGColor
	boxf 0, 0, 320, 280

	objmode 2, 1
	setColor iniFGColor

	pos 16, 16
	font iniFont, 16
	setColor iniFGColor
	mes "起動方法"
	pos 88, 14
	font iniFont, 12
	settingLocker1 = 0
	objsize 80, 20
	combox settingLocker1, 40, "マウス左\nマウス右\nマウス中\n拡張1\n拡張2"
	idSettingLocker1 = stat
	objprm idSettingLocker1, getLockerKey( iniLocker1 )
	pos 172, 16
	font iniFont, 16
	mes "→"
	pos 192, 14
	font iniFont, 12
	settingLocker2 = 0
	objsize 80, 20
	combox settingLocker2, 40, "マウス左\nマウス右\nマウス中\n拡張1\n拡張2"
	idSettingLocker2 = stat
	objprm idSettingLocker2, getLockerKey( iniLocker2 )

	pos 16, 40
	font iniFont, 16
	setColor iniFGColor
	mes "文字色"
	setColor iniFGColor ^ $FFFFFF :boxf 88, 40, 104, 56
	setColor iniFGColor           :boxf 89, 41, 103, 55
	pos 112, 38
	objsize 32, 20
	font iniFont, 12
	button gosub "変更", *setfgcolor

	pos 16, 64
	font iniFont, 16
	setColor iniFGColor
	mes "背景色"
	setColor iniBGColor ^ $FFFFFF :boxf 88, 64, 104, 80
	setColor iniBGColor           :boxf 89, 65, 103, 79
	pos 112, 62
	objsize 32, 20
	font iniFont, 12
	button gosub "変更", *setbgcolor

	pos 16, 88
	font iniFont, 16
	setColor iniFGColor
	mes "フォント"
	pos 88, 88
	font iniFont, 12
	settingFont = iniFont
	input settingFont, 176, 16, 1024
	idSettingFont = stat
	sendmsg objinfo(idSettingFont, 2), $CF, 1 ; 編集不可
	pos 272, 86
	objsize 32, 20
	button gosub "変更", *setfont

	pos 16, 112
	font iniFont, 16
	setColor iniFGColor
	mes "ファイラ"
	pos 88, 112
	font iniFont, 12
	settingFiler = iniFiler
	input settingFiler, 176, 16, 1024
	idSettingFiler = stat
	sendmsg objinfo(idSettingFiler, 2), $CF, 1 ; 編集不可
	pos 272, 110
	objsize 32, 20
	button gosub "変更", *setfiler

	pos 16, 136
	font iniFont, 16
	setColor iniFGColor
	mes "起動時にタイトルを"

	pos 168, 134
	font iniFont, 12
	objsize 96, 20
	combox settingSplash, 40, "表示しない\n表示する"
	idSettingSplash = stat
	objprm idSettingSplash, iniSplash

	pos 16, 160
	font iniFont, 16
	setColor iniFGColor
	mes "ホイールスクロール量"

	pos 88, 182
	winobj "msctls_trackbar32", "", , $50000000 | $1, 176, 20
	objSettingWheel = objinfo(stat, 2)
	sendmsg objSettingWheel, $405, 1, iniWheel ; 初期つまみ位置
	sendmsg objSettingWheel, $414, 10          ; バーの間隔
	oncmd gosub *setwheel, $114

	pos 88, 208
	font iniFont, 16
	objsize 80, 32
	button gosub "閉じる", *closesetting

	pos 152 - ( 8 * strlen( version )), 256
	font iniFont, 16
	mes "Mulancha " + version + " by "

	pos 256, 254
	font iniFont, 12
	objsize 48, 20
	button gosub "m-ushi", *gotomunet

	return

*settingedit
	gsel 0
	EnableWindow hwnd, 0 ; 自ウィンドウを無効にする。

	gsel 4, -1
	width , , ginfo_mx - 160, ginfo_my - 120
	gsel 4, 2

	chdir curdir
	noteload "mlc2.ini"

	changebgcolor = 0
	onnstat = 0

	repeat
		getkey ev, 27
		if (ev == 1)|(oncstat == 1) {
			gosub *setlocker
			if oncstat == 1 :break
		}
		; 多重起動の通知があれば前面表示
		if ( onnstat == 1 ) {
			gsel 4, 2
			onnstat = 0
		}

		wait 5
	loop
	oncstat = 0

	if notemax < 8 {
		noteadd "splash=" + settingSplash, -1, 0
	} else {
		noteadd "splash=" + settingSplash, 7, 1
	}
	iniSplash = settingSplash

	chdir curdir
	notesave "mlc2.ini"

	if ( changebgcolor == 1 ) {
		chdir curdir + "\\data"
		dirlist temp, "*.bmp"
		split temp, "\n", array
		repeat stat - 1
			delete array(cnt)
		loop
	}

	gsel 4, -1
	gsel 0, 2
	EnableWindow hwnd, 1

	return

*setlocker
	if settingLocker1 == settingLocker2 {
		dialog "起動方法は\n別々のボタンを組み合わせて下さい"
		if oncstat == 1 :oncstat = 0
	} else {
		iniLocker1 = getLockerValue( settingLocker1 )
		iniLocker2 = getLockerValue( settingLocker2 )
		noteadd "locker1=" + iniLocker1, 1, 1
		noteadd "locker2=" + iniLocker2, 2, 1
	}
	return

*setfgcolor
	dialog "", 33
	if stat == 1 {
		iniFGColor = ( ginfo_r << 16 ) + ( ginfo_g << 8 ) + ginfo_b
		setColor iniFGColor ^ $FFFFFF :boxf 88, 40, 104, 56
		setColor iniFGColor           :boxf 89, 41, 103, 55

		noteadd "fgcolor=#" + strf("%06x", iniFGColor), 5, 1
	}
	return

*setbgcolor
	dialog "", 33
	if stat == 1 {
		iniBGColor = ( ginfo_r << 16 ) + ( ginfo_g << 8 ) + ginfo_b
		setColor iniBGColor ^ $FFFFFF :boxf 88, 64, 104, 80
		setColor iniBGColor           :boxf 89, 65, 103, 79

		noteadd "bgcolor=#" + strf("%06x", iniBGColor), 6, 1
		changebgcolor = 1
	}
	return

*setfont
	dim tempArray, 8
	fontdlg tempArray, 0
	if stat != 0 {
		iniFont = refstr
		objprm idSettingFont, iniFont
		noteadd "font=" + iniFont, 4, 1
	}
	return

*setfiler
	dialog "", 16
	if stat == 1 {
		iniFiler = refstr
		objprm idSettingFiler, iniFiler
		noteadd "filer=" + iniFiler, 3, 1
	}
	return

*setwheel
	if lparam == objSettingWheel {
		sendmsg objSettingWheel, $400
		iniWheel = stat
		if notemax < 8 {
			noteadd "splash=" + settingSplash, -1, 0
			noteadd "wheel=" + iniWheel, -1, 0
		} else : if notemax == 8 {
			noteadd "wheel=" + iniWheel, -1, 0
		} else {
			noteadd "wheel=" + iniWheel, 8, 1
		}
	}
	return

*closesetting
	oncstat = 1
	return

*gotomunet
	exec "https://munet.x0.to/", 16
	return